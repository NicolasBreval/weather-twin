plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.graalvm.buildtools.native' version '0.11.3'
	id 'io.freefair.lombok' version '9.1.0'
	id 'antlr'
}

group = 'org.nbreval.weather-twin'
version = '0.0.1'
description = "Gateway used to process and redirect device's sensors information"

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

repositories {
	mavenCentral()
}

ext {
	set('mapDbVersion', "3.1.0")
	set('kryoVersion', "5.6.2")
	set('eclipseCaliforniumVersion', "3.14.0")
	set('antlrVersion', "4.13.2")
	set('springdocVersion', "3.0.0")
}

configurations {
    all*.exclude group: 'org.slf4j', module: 'slf4j-reload4j'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation "org.mapdb:mapdb:${mapDbVersion}"
	implementation "com.esotericsoftware:kryo:${kryoVersion}"
	implementation "org.eclipse.californium:californium-core:${eclipseCaliforniumVersion}"
	implementation "org.springdoc:springdoc-openapi-starter-webflux-ui:${springdocVersion}"
	antlr "org.antlr:antlr4:${antlrVersion}"
	implementation "org.antlr:antlr4-runtime:${antlrVersion}"
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

def antlrGeneratedSrcDic = file('src/main/java/org/nbreval/weather_twin/gateway/infrastructure/util/wtal')
def antlrPackageName = 'org.nbreval.weather_twin.gateway.infrastructure.util.wtal'

generateGrammarSource {
	outputDirectory = antlrGeneratedSrcDic

	arguments = [
		'-package', antlrPackageName,
		'-visitor'
	]
}

sourceSets.main {
	antlr {
		srcDir 'src/main/antlr'
	}
}

clean {
	delete antlrGeneratedSrcDic
}